if(NOT CMAKE_CROSSCOMPILING)
  find_package(PkgConfig QUIET)
  pkg_check_modules(PC_WEBSOCKETS libwebsockets)
endif()

set_extra_dirs_lib(WEBSOCKETS libwebsockets)
find_library(WEBSOCKETS_LIBRARY
  NAMES websockets
  HINTS ${HINTS_WEBSOCKETS_LIBDIR} ${PC_WEBSOCKETS_LIBDIR} ${PC_WEBSOCKETS_LIBRARY_DIRS}
  PATHS ${PATHS_WEBSOCKETS_LIBDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH}
)
set_extra_dirs_include(WEBSOCKETS libwebsockets "${WEBSOCKETS_LIBRARY}")
find_path(WEBSOCKETS_INCLUDEDIR
  NAMES libwebsockets.h
  HINTS ${HINTS_WEBSOCKETS_INCLUDEDIR} ${PC_WEBSOCKETS_INCLUDEDIR} ${PC_WEBSOCKETS_INCLUDE_DIRS}
  PATHS ${PATHS_WEBSOCKETS_INCLUDEDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH}
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Websockets DEFAULT_MSG WEBSOCKETS_LIBRARY WEBSOCKETS_INCLUDEDIR)

mark_as_advanced(WEBSOCKETS_LIBRARY WEBSOCKETS_INCLUDEDIR)

if(WEBSOCKETS_FOUND)
  set(WEBSOCKETS_LIBRARIES ${WEBSOCKETS_LIBRARY})
  set(WEBSOCKETS_INCLUDE_DIRS ${WEBSOCKETS_INCLUDEDIR})

  add_library(Deps::Websockets UNKNOWN IMPORTED)
  set_target_properties(Deps::Websockets PROPERTIES
    IMPORTED_LOCATION "${WEBSOCKETS_LIBRARY}"
    INTERFACE_INCLUDE_DIRECTORIES "${WEBSOCKETS_INCLUDEDIR}"
  )

  is_bundled(WEBSOCKETS_BUNDLED "${WEBSOCKETS_LIBRARY}")
endif()
